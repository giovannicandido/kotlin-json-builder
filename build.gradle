group = "codes.matteo"
version = "1.0"
project.ext.archivesBaseName = "kotlin-json-builder"

buildscript {
    repositories {
        mavenCentral()
        maven {
            setUrl("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.21"
        classpath("me.champeau.gradle:jmh-gradle-plugin:0.4.4")
    }
}


repositories {
    mavenCentral()
}

apply plugin: "kotlin"
apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "me.champeau.gradle.jmh"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.21"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.2.21"
    compile "com.fasterxml.jackson.core:jackson-core:2.9.4"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.4"
    testCompile 'junit:junit:4.12'

    jmh("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4") {
        exclude group: "org.jetbrains.kotlin"
    }
    jmh group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.19'

}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jmh {
    include = ['.*']
    jvmArgs = ['-Djmh.ignoreLock=true']
    includeTests = false
    duplicateClassesStrategy = 'warn'
    fork = 2
    warmupIterations = 10
    iterations = 10
    zip64 = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    required = true
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Kotlin JSON Builder'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A Kotlin typesafe builder for Jackson JsonNodes'
                url 'https://github.com/matteobanerjee/kotlin-json-builder'

                scm {
                    connection 'scm:git:https://github.com/matteobanerjee/kotlin-json-builder.git'
                    developerConnection 'scm:git:https://github.com/matteobanerjee/kotlin-json-builder.git'
                    url 'https://github.com/matteobanerjee/kotlin-json-builder'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id 'matteobanerjee'
                        name 'Matteo Banerjee'
                        email 'self@matteo.codes'
                    }
                }
            }
        }
    }
}